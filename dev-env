#!/usr/bin/env fish

set script_dir (pwd)
set dry 0

# Parse arguments
for arg in $argv
    if test "$arg" = --dry
        set dry 1
    end
end

function log
    if test $dry -eq 1
        echo "[DRY_RUN]: $argv"
    else
        echo "$argv"
    end
end

function execute
    log "execute $argv"
    if test $dry -eq 1
        return
    end
    command $argv
end

log "------------- setting up dev-env -------------"

function copy_dir
    set from $argv[1]
    set to $argv[2]

    for dir in (find $from -mindepth 1 -maxdepth 1 -type d)
        set name (basename $dir)
        execute rm -rf "$to/$name"
        execute cp -r "$dir" "$to/$name"
    end
end

function copy_file
    set from $argv[1]
    set to $argv[2]
    set name (basename $from)

    execute rm -f "$to/$name"
    execute cp "$from" "$to/$name"
end

copy_file ./env/.zshrc $HOME
copy_file ./env/tmux-sessionizer.sh $HOME/.local/bin
copy_dir ./env/.config $HOME/.config
copy_file ./env/config.fish $HOME/.config/fish
